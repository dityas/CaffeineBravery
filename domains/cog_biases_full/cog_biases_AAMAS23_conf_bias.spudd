(defvar HostType        (critical honeypot critical_honeypot))
(defvar Round           (r1 r2))

(defvar Attl1ModelsDefl0    (m0 m1 m2 m3))
(defvar Attl1ModelsDefl0EC  (ec0))

(defvar AttackerFrame   (att))
(defvar DefFrame        (highCap lowCap))

-- Actions for both agents
(defvar DefActions      (Deceive Observe Nop))
(defvar AttActions      (Attack Exit Nop))

(defvar HpObs           (yes no))
(defvar HostTypeObs     (c hp))
(defvar DefenderAction  (deceive none))

--------------------------------------------------------------------


(defdd defenderActionObs
    (DefActions

        (Deceive
            (DefenderAction'
                (deceive    (0.85))
                (none       (0.15))
            )
        )

        (Observe
            (DefenderAction'
                (deceive    (0.15))
                (none       (0.85))
            )
        )

        (Nop
            (DefenderAction'
                (deceive    (0.15))
                (none       (0.85))
            )
        )
    )
)

(defdd hostTypeObs
    (HostType'
        (critical           
            (HostTypeObs'
                (c      (0.99))
                (hp     (0.01))
            )
        )
        
        (honeypot           
            (HostTypeObs'
                (c      (0.01))
                (hp     (0.99))
            )
        )

        (critical_honeypot  
            (HostTypeObs' UNIFORM)
        )
    )
)

(defdd hpObs
    (HostType'
        (critical   
            (HpObs'
                (yes    (0.01))
                (no     (0.99))
            )
        )
        (honeypot
            (HpObs'
                (yes    (0.99))
                (no     (0.01))
            )
        )
        (critical_honeypot  (HpObs' UNIFORM))
    )
)

(defdd roundTrans
    (Round
        (r1     (Round' r2))
        (r2     (Round' r2))
    )
)

(defdbn actionDefender
    (HostType   (SAME HostType))
    (Round      (roundTrans))
    (HpObs      (hpObs)) 
)

(defdd cReward
    (HostType
        (critical           (1.0))
        (honeypot           (-1.0))
        (critical_honeypot  (1.0))
    )
)

(defdd hpReward
    (HostType
        (critical           (-1.0))
        (honeypot           (1.0))
        (critical_honeypot  (0.0))
    )
)

-- defender L1 model
(defpomdp defl0_active
    (S
        (HostType Round)
    )

    (O
        (HpObs)
    )

    (A  DefActions)

    (dynamics
        (Deceive    (actionDefender))
        (Observe    (actionDefender))
        (Nop        (actionDefender))
    )
    
    (R
        (Deceive    (hpReward))
        (Observe    (cReward))
        (Nop        (0.0))
    )

    (discount 0.95)
)

(defdd initDefl0_active
    (HostType           
        (critical           (0.5))
        (honeypot           (0.5))
        (critical_honeypot  (0.0))
    )
    * (Round r1)
)

------ Initial beliefs of l1 defender

(defdd initDefl0c_active
    (HostType
        (critical           (1.0))
        (honeypot           (0.0))
        (critical_honeypot  (0.0))
    )
    * (Round r1)
)

(defdd initDefl0hp_active
    (HostType
        (critical           (0.0))
        (honeypot           (1.0))
        (critical_honeypot  (0.0))
    )
    * (Round r1)
)


-- Make passive defender model

-- passive defender l1 model
(defpomdp defl0_passive
    (S
        (HostType Round)
    )

    (O
        (HpObs)
    )

    (A  DefActions)

    (dynamics
        (Deceive    (actionDefender))
        (Observe    (actionDefender))
        (Nop        (actionDefender))
    )
    
    (R
        (Deceive    (0.0))
        (Observe    (1.0))
        (Nop        (0.0))
    )

    (discount 0.95)
)

(defdd initDefl0_passive
    (HostType           
        (critical           (0.5))
        (honeypot           (0.5))
        (critical_honeypot  (0.0))
    )
    * (Round r1)
)

(defdd initDefl0c_passive
    (HostType
        (critical           (1.0))
        (honeypot           (0.0))
        (critical_honeypot  (0.0))
    )
    * (Round r1)
)

(defdd initDefl0hp_passive
    (HostType
        (critical           (0.0))
        (honeypot           (1.0))
        (critical_honeypot  (0.0))
    )
    * (Round r1)
)

-- Attacker l2
(initmodelvar Attl1ModelsDefl0
    (frames DefFrame)
    (
        (lowCap     (m0 (initDefl0c_passive)))
        (lowCap     (m1 (initDefl0hp_passive)))
        (highCap    (m2 (initDefl0c_active)))
        (highCap    (m3 (initDefl0hp_active)))
    )
)

(defdbn actionAttacker
    (HostType       (SAME HostType))
    (Round          (roundTrans))

    (HostTypeObs    (hostTypeObs))
    (DefenderAction (defenderActionObs))
)

-- Level 1 Attacker model
(defipomdp attl1
    (S
        (HostType Round)
    )

    (O
        (HostTypeObs DefenderAction)
        --(DefenderAction)
    )

    (A  AttActions)
    (Aj DefActions)

    (Mj Attl1ModelsDefl0)
    (EC Attl1ModelsDefl0EC)

    (Thetaj DefFrame
        (lowCap    defl0_passive)
        (highCap   defl0_active)
    )

    (dynamics
        (Attack     (actionAttacker))
        (Exit       (actionAttacker))
        (Nop        (actionAttacker))
    )

    (R
        (Attack     (cReward))
        (Exit       (0.0))
        (Nop        (hpReward))
    )

    (discount 0.95)
    (H 10)
)

(defdd dependentMjAttl1
    (HostType
        (critical
            (Attl1ModelsDefl0
                (m0     (0.5))
                (m1     (0.0))
                (m2     (0.5))
                (m3     (0.0))
            )      
        )

        (honeypot
            (Attl1ModelsDefl0
                (m0     (0.0))
                (m1     (0.5))
                (m2     (0.0))
                (m3     (0.5))
            )
        )

        (critical_honeypot
            (Attl1ModelsDefl0
                (m0     (0.3333))
                (m1     (0.3333))
                (m2     (0.0))
                (m3     (0.3334))
        )
    )
)

(defdd initAttl1
    (HostType   
        (critical           (0.0))
        (honeypot           (0.0))
        (critical_honeypot  (1.0))
    )
    * (dependentMjAttl1)
    * (Round r1)
)

(defdd initS
    (HostType   honeypot)
    * (Round r1)
)
